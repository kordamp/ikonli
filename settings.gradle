/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2015-2024 Andres Almiray
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
pluginManagement {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        mavenCentral()
    }
    plugins {
        id 'org.kordamp.gradle.guide'   version kordampPluginVersion
        id 'org.kordamp.gradle.bom'     version kordampPluginVersion
        id 'org.ajoberstar.git-publish' version gitPluginVersion
        id 'com.google.osdetector'      version osPluginVersion
        id 'org.beryx.jlink'            version jlinkPluginVersion
        id 'org.jreleaser.jdks'         version jreleaserPluginVersion
        id 'biz.aQute.bnd.builder' 		version bndPluginVersion
    }
}

buildscript {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath "org.kordamp.gradle:kordamp-parentbuild:$kordampBuildVersion"
    }
}
apply plugin: 'org.kordamp.gradle.kordamp-parentbuild'

rootProject.name = 'ikonli'

projects {
    directories = ['docs', 'core', 'icon-packs', 'apps']
}

enforce {
    mergeStrategy = 'append'

    rule(enforcer.rules.BanDuplicateClasses) { r ->
        r.ignoreWhenIdentical.set(true)
        r.dependency("com.google.guava:guava:$guavaVersion".toString()) { d->
            d.ignore('com.google.common.*')
        }
    }

    rule(enforcer.rules.ForceDependencies) { r ->
        r.dependencies.addAll "com.google.errorprone:error_prone_annotations:$errorproneVersion",
            "com.google.guava:guava:$guavaVersion"
    }
}